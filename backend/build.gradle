import org.springframework.cloud.contract.verifier.config.TestFramework
import org.springframework.cloud.contract.verifier.config.TestMode
import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id "org.springframework.boot" version "${springBootVersion}"
	id "org.springframework.cloud.contract" version "${springCloudContractPluginVersion}"
	id 'java'
	id 'jacoco'
	id 'org.checkerframework' version '0.5.12'
}

apply plugin: 'spring-cloud-contract'

group = 'io.spring.cloud.samples.animalrescue'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

checkerFramework {
	excludeTests = true
	checkers = [
			'org.checkerframework.checker.nullness.NullnessChecker',
	]
}

repositories {
	mavenCentral()
}

dependencies {
	implementation enforcedPlatform(SpringBootPlugin.BOM_COORDINATES)
	implementation enforcedPlatform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")

	developmentOnly enforcedPlatform(SpringBootPlugin.BOM_COORDINATES)
	developmentOnly "org.springframework.boot:spring-boot-devtools"

	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    runtimeOnly 'com.h2database:h2'

	testImplementation('org.springframework.security:spring-security-test')
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
	testImplementation 'io.rest-assured:spring-web-test-client'
}

contracts {
	testFramework = TestFramework.JUNIT5
	testMode = TestMode.WEBTESTCLIENT
	baseClassForTests = "io.spring.cloud.samples.animalrescue.contracts.BaseClass"

	contractsMode = "LOCAL"
	contractDependency {
		stringNotation = "io.spring.cloud.samples.animalrescue:contracts"
	}
}

copyContracts.dependsOn(":contracts:install")

test {
	useJUnitPlatform()
	testLogging {
		exceptionFormat = "full"
		events = ["passed", "skipped", "failed"]
	}
}

jacocoTestReport {
	dependsOn test
    reports {
        xml.enabled true
		xml.destination new File("${buildDir}/reports/jacoco/report.xml")
		html.enabled false
		csv.enabled false
    }
}
